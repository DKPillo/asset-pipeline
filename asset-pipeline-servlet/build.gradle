buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
	}
}

plugins {
	id 'com.jfrog.bintray' version '1.2'
}


apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
sourceCompatibility = '1.7'
targetCompatibility = '1.7'
repositories {
	mavenCentral()
}

configurations {
	provided
	doc.extendsFrom(runtime)
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		groovy.srcDirs   += ['src/main/java']
		java.srcDirs      = []
	}
}

dependencies {
	provided    'org.codehaus.groovy:groovy:2.0.7'
	compile     project(':asset-pipeline-core')
	compile 'javax.servlet:javax.servlet-api:3.1.0'
	testCompile 'org.codehaus.groovy:groovy:2.0.7'
    testCompile 'org.eclipse.jetty:jetty-server:9.2.10.v20150310'
    testCompile 'org.eclipse.jetty:jetty-servlet:9.2.10.v20150310'
    testCompile 'org.eclipse.jetty:jetty-webapp:9.2.10.v20150310'
    testCompile "org.apache.httpcomponents:httpclient:4.3.4"
    testCompile "org.apache.httpcomponents:fluent-hc:4.3.4"
    testCompile 'junit:junit:4.10'
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId 'asset-pipeline-servlet'
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'asset-pipeline-servlet'
					description 'JVM Asset Pipeline Servlet Adapter.'
					url 'https://github.com/bertramdev/asset-pipeline-core'
					scm {
						url 'https://github.com/bertramdev/asset-pipeline-core'
						connection 'scm:https://bertramdev@github.com/bertramdev/asset-pipeline-core.git'
						developerConnection 'scm:git://github.com/bertramdev/asset-pipeline-core.git'
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'davydotcom'
							name 'David Estes'
							email 'davydotcom@gmail.com'
						}
					}
				}
			}
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}

	repositories {
		maven {
			credentials {
				username project.properties.get('artifactoryPublishUsername')
				password project.properties.get('artifactoryPublishPassword')
			}
			url 'https://repo.grails.org/grails/libs-snapshots-local/'
		}
	}
}

bintray {
	if(project.hasProperty('bintrayUser')) {
		user = bintrayUser
		key = bintrayKey
	}
	publications = ['maven']
	pkg {
		repo = 'asset-pipeline'
		userOrg = 'bertramlabs'
		name = 'asset-pipeline-servlet'
		licenses = ['Apache-2.0']
		websiteUrl = 'https://github.com/bertramdev/asset-pipeline-core'
        issueTrackerUrl = 'https://github.com/bertramdev/asset-pipeline-core/issues'
        vcsUrl = 'https://github.com/bertramdev/asset-pipeline-core.git'
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.runtimeClasspath
}



bintrayUpload.dependsOn build, sourcesJar, javadocJar