apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.bertramlabs.plugins'
version = '2.0.0'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}


repositories {
    jcenter()
    mavenCentral()

}


dependencies {
    // compile 'org.codehaus.groovy:groovy-all:2.2.2'
    compile gradleApi()
    compile localGroovy()

    compile 'org.mozilla:rhino:1.7R4'
    compile 'log4j:log4j:1.2.17'

    compile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude group:'org.codehaus.groovy'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'asset-pipeline-core'

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['maven']
    pkg {
        repo = 'asset-pipeline'
        userOrg = 'bertramlabs'
        name = 'asset-pipeline-core'
        licenses = ['Apache-2.0']
    }
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}


bintrayUpload.dependsOn build, sourcesJar, javadocJar
