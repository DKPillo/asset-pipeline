buildscript {
  repositories {
    mavenLocal()
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.2.0"
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    classpath "com.bertramlabs.plugins:morpheus-gradle:0.1.2"
//    classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.7.5"
  }
}

 plugins {
   id "com.bertramlabs.asset-pipeline" version "2.7.2"
 }


apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "idea"
apply plugin: 'asset-pipeline'
apply plugin: 'morpheus'

repositories {
  jcenter()
}


dependencies {
  // Default SLF4J binding.  Note that this is a blocking implementation.
  // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
  runtime 'org.slf4j:slf4j-simple:1.7.12'

  assets project(':asset-pipeline-core')
  assets project(':ratpack-asset-pipeline')
  assets project(':less-asset-pipeline')
  runtime 'org.webjars:d3js:3.5.16'
  testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

assets {
  excludes = ["**/*.less"]
  includes = ["application.less"]
}
// project(':common:shared').projectDir

task copyApiDoc(type: Copy, dependsOn: ':asset-pipeline-core:groovydoc') {
    from new File(project(':asset-pipeline-core').buildDir,'/docs/groovydoc')
    into 'src/assets/html/apidoc/'
}

task copyManual(type: Copy, dependsOn: ':asset-pipeline-grails:docs') {
    from new File(project(':asset-pipeline-grails').buildDir,'/docs/manual')
    into 'src/assets/html/manual/'
}

morpheus {
  if(project.hasProperty('morpheusUser')) {
      morpheusUser = project.morpheusUser
      morpheusPassword = project.morpheusPassword
  }
  applianceUrl = 'https://v2.morpheusdata.com'
  instance 'Asset-Pipeline Site'
  from {
    resolverPath = new File(project(':asset-pipeline-site').buildDir, '/libs').canonicalPath
    includes = ["asset-pipeline-site-${version}-all.jar"]
  }
  deployConfiguration = [
    javaCommand: "-Dratpack.port=8080 -jar asset-pipeline-site-${version}-all.jar"
  ]
}

tasks.morpheusDeploy.dependsOn tasks.shadowJar
tasks.processResources.dependsOn copyApiDoc
tasks.processResources.dependsOn copyManual
// tasks.assetCompile.dependsOn copyManual
